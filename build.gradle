/*
 * Copyright (c) 2019 Zac Sweers
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id "org.jetbrains.kotlin.jvm" apply false
  id "org.jetbrains.kotlin.kapt" apply false
  id "com.vanniktech.maven.publish" version "0.11.1" apply false
}

subprojects {
  apply from: rootProject.file("gradle/dependencies.gradle")
  repositories {
    mavenCentral()
    jcenter()
    maven {
      url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
  }
  pluginManager.withPlugin("org.jetbrains.kotlin.jvm") {
    compileKotlin {
      kotlinOptions {
        jvmTarget = deps.build.kotlin.jvmTarget
        freeCompilerArgs += deps.build.kotlin.defaultFreeCompilerArgs
      }
    }

    compileTestKotlin {
      kotlinOptions {
        jvmTarget = deps.build.kotlin.jvmTarget
        freeCompilerArgs += deps.build.kotlin.defaultFreeCompilerArgs
      }
    }
  }
  pluginManager.withPlugin("org.jetbrains.kotlin.kapt") {
    kapt {
      // Compiling with JDK 11+, but kapt doesn"t forward source/target versions.
      // TODO fixed in Kotlin 1.3.60
      javacOptions {
        option("-source", "8")
        option("-target", "8")
      }
    }
  }
}

apply from: "gradle/dependencies.gradle"
